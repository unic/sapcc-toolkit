plugins {
	id 'org.springframework.boot' version '2.6.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
}

group = 'com.unic.sapcc.toolkit'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(16)
	}
}

repositories {
	mavenCentral()
}

publishing {
	publications {
		sapccToolkit(MavenPublication) {
			artifact bootJar
		}
	}
	repositories {
		maven {
			name = 'unicEcom'
			credentials(PasswordCredentials)
			url 'https://nexus.unic.com/nexus/repository/unic-ecom-' + (version.toString().endsWith('-SNAPSHOT') ? 'snapshots' : 'releases')
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'junit:junit:4.13.2'
    implementation 'commons-cli:commons-cli:1.5.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
	useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property 'sonar.branch.name', "${project.hasProperty('sonarbranch') ? sonarbranch : ''}"
        property 'sonar.branch.target', "${project.hasProperty('sonarbranchtarget') ? sonarbranchtarget : ''}"
        property 'sonar.host.url', 'https://sonar.service.unic24.net'
        property 'sonar.login', project.hasProperty('unicSonarUser') ? unicSonarUser : System.properties['sonar.login'] ?: 'undefined'
        property 'sonar.projectVersion', "$project.version"
    }
}